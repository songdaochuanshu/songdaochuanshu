---
layout: post
title: '写个隐藏磁盘的小工具'
date: "2026-01-17T00:44:10Z"
---
写个隐藏磁盘的小工具
==========

一、手工操作
------

1.  打开注册表，定位至`HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer`
2.  在右侧窗口中新建一个类型为`DOWRD（32位）`的键值，将其命名为`NoDrives`
3.  计算需要隐藏的磁盘驱动器的数值，A盘的值为2的0次方，B盘的值为2的1次方，C盘的值为2的2次方，依此类推。若需隐藏多个磁盘驱动器，则将多个磁盘驱动器的值相加
4.  修改`NoDrives`键值数据，基数选择十进制，数值数据填写为刚才计算得到的值
5.  打开任务管理器，重启`Explorer.exe`（Windows 资源管理器）

二、使用脚本
------

### 1、使用脚本操作注册表

1.  以管理员权限打开命令提示符
    
2.  输入以下命令即可替代手工操作中的步骤1、2、4，命令中`/d`后面的`4`即对应C盘
    

    reg add HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer /v NoDrives /t REG_DWORD /d 4 /f
    

### 2、使用脚本计算单个盘符对应的值

复制以下代码到记事本中，并将其另存为“盘符数值计算.cmd”。（选择ANSI编码，文件名后面使用.cmd，文件类型选择\*.\*）

    @echo off
    setlocal enabledelayedexpansion
    set /p drive=请输入一个盘符（仅字母）：
    set n=0
    for %%i in (A B C D E F G H I J K L M N O P Q R S T U V W X Y Z) do (
        set /a bn=1^<^<!n!
        if /i %%i equ !drive! (
            echo %%i = !bn!
        )
        set /a n+=1
    )
    pause
    exit
    

运行批处理，假如你输入的是`D`，回车后，你应该得到`D=8`的输出显示

![image](https://img2024.cnblogs.com/blog/3738145/202601/3738145-20260116143817613-1895772585.png)

### 3、调整代码使其支持多个盘符数值的自动计算

使用以下代码，覆盖之前的cmd文件代码

    @echo off
    setlocal enabledelayedexpansion
    set /p drives=请输入需要隐藏的盘符(仅字母)，以空格作为分隔：
    for %%d in (!drives!) do call :calc %%d
    setlocal disabledelayedexpansion
    pause
    exit
    
    :calc
    set n=0
    for %%i in (A B C D E F G H I J K L M N O P Q R S T U V W X Y Z) do (
        set /a bn=1^<^<!n!
        if /i %%i equ %1 (
            echo %%i = !bn!
        )
        set /a n+=1
    )
    goto :eof
    

运行批处理文件，输入`D E F`，你将能看到它可以输出每个盘符对应的数值

![image](https://img2024.cnblogs.com/blog/3738145/202601/3738145-20260116143853530-1657282542.png)

### 4、完善代码使其可以自动累加并添加到注册表

完整的批处理代码，温馨提醒：请使用管理员权限运行

    :: 隐藏磁盘
    @echo off
    setlocal enabledelayedexpansion
    set sn=0
    set /p drives=请输入需要隐藏的盘符(仅字母)，以空格作为分隔：
    for %%d in (!drives!) do call :calc %%d
    echo 合计!sn!
    reg add HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer /v NoDrives /t REG_DWORD /d !sn! /f
    setlocal disabledelayedexpansion
    echo.
    echo 需要重启Explorer.exe使其生效
    pause
    exit
    
    :calc
    set n=0
    for %%i in (A B C D E F G H I J K L M N O P Q R S T U V W X Y Z) do (
        set /a bn=1^<^<!n!
        if /i %%i equ %1 (
            echo %%i = !bn!
            set /a sn+=!bn!
        )
        set /a n+=1
    )
    goto :eof
    

如果需要自动重启`Explorer.exe`可以在命令提示符窗口中单独使用以下命令

    taskkill /f /im "explorer.exe" && start "explorer.exe"
    

也可以在批处理文件的`pause`命令后面敲击回车，另起一行后粘贴此命令，效果如下：

    rem 之前的代码不变...
    pause
    taskkill /f /im "explorer.exe" && start "explorer.exe"
    exit
    rem 之后的代码不变...
    

三、最后为了美观，使用C#写了一个GUI工具
----------------------

1、工具预览
------

![image](https://img2024.cnblogs.com/blog/3738145/202601/3738145-20260116123804386-330758985.png)

2、下载须知
------

工具使用.NET 10框架编写，故而电脑中若没有此运行库，可能会提示你下载

![image](https://img2024.cnblogs.com/blog/3738145/202601/3738145-20260116123826339-2133118964.jpg)

点击Download it now下载并安装运行库即可

本工具下载地址：[点我下载](https://files.cnblogs.com/files/blogs/858015/HideDrives-GUI.rar?t=1768538374&download=true)

本文来自博客园，作者：[xlifevr](https://www.cnblogs.com/xiuxing-lab/)，转载请注明原文链接：[https://www.cnblogs.com/xiuxing-lab/p/19460261](https://www.cnblogs.com/xiuxing-lab/p/19460261)