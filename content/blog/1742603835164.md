---
layout: post
title: 'Oracle存储过程里操作BLOB的字节数据的办法'
date: "2025-03-22T00:37:15Z"
---
Oracle存储过程里操作BLOB的字节数据的办法
=========================

一、缘由
----

BLOB是指二进制大对象，也就是英文Binary Large Object的缩写。  
在很多时候，我们是通过其他编程语言（如Java）访问BLOB的字节数据，进行字节级的操作的。  
但是有些时候工作量很小，感觉专门为BLOB字节级操作而专门开发个程序，是比较麻烦的。于是我研究了一下如何直接在Oracle存储过程里操作BLOB的字节数据。

二、办法
----

2.1 基本操作
--------

使用 length 函数，可以获取blob的字节长度。如 `v_len := length(i_blob);` 。  
与字符串（如 varchar2 等）一样，blob为 `null` 时，length的返回值是 `null`。故建议加上 nvl 做一下转换，如 `v_len := nvl(length(i_blob), 0);` 。

为了避免 `null` 问题，可使用 empty\_blob 函数，它的作用是返回一个长度为0的blob。如 `v_blob := empty_blob();` 。

empty\_blob返回的blog只是一个初始化，它是不能修改字节数据的。于是需要使用 dbms\_lob.createtemporary 来创建一个能进行字节数据操作的临时blob。如 `dbms_lob.createtemporary(v_blob, TRUE);` 。

2.2 DBMS\_LOB包
--------------

为了便于BLOB类型的使用，Oracle官方提供了 DBMS\_LOB 包，它提供了很多工具函数。例如先前我们使用了createtemporary函数。  
DBMS\_LOB所提供的过程有——

*   APPEND：将源LOB中的内容加到目的LOB中。
*   CLOSE：关闭已经打开的LOB。
*   CREATETEMPORARY：在用户的临时表空间中，建立临时LOB。
*   FILECLOSE：关闭打开的BFILE定位符所指向的OS文件。
*   FILECLOSEALL：关闭当前会话已经打开的所有BFILE文件。
*   FILEEXISTS：确定file\_loc对应的OS文件是否存在，1：存在。0：不存在。
*   FILEGETNAME：获取BFILE定位符所对应的目录别名和文件名。
*   FILEISOPEN：确定BFILE对应的OS文件是否打开。
*   FREETEMPORARY：释放在默认临时表空间中的临时LOB。
*   FILEOPEN：打开文件。
*   GETCHUNKSIZE：当建立包含CLOB/BLOB列的表时，通过指定CHUNK参数可以指定操纵LOB需要分配的字节数（数据库尺寸的整数倍）默认为数据块的尺寸。
*   COPY：从源LOB中复制数据到目的LOB。
*   ERASE：删除LOB中全部或部分内容。
*   TRIM：将LOB值减少到指定的长度。
*   WRITE：向LOB中写入数据。
*   INSTR：返回特定样式数据从LOB某偏移位置开始出现N次的具体位置。
*   IDOPEN：确定LOB是否打开，打开：1，未打开：0。
*   ISTEMPORARY：确定定位符是否为临时LOB。
*   LOADFROMFILE：将BFILE的部分或全部内容复制到目标LOB变量。
*   LOADBLOBFROMFILE：将BFILE数据装载到BLOB中，并且在装载后取得最新的偏移位置。
*   OPEN：打开LOB，open\_mode（只读：dbms\_lob.lob\_readonly，写：dbms\_lob.lob\_readwrite）。
*   COMPARE：比较两个同种数据类型的LOB的部分或全部值是否相同。
*   GETLENGTH：获取LOB的长度。
*   READ：从LOB中读出数据。
*   SUBSTR：与字符处理函数SUBSTR使用方法一样。
*   WRITEAPPEND：将缓冲区数据写到LOB尾部。

有了DBMS\_LOB包后，对于（变量级的）BLOB操作就比较方便了。例如我们想将两个blob的内容，连续拼接到1个blob中，则可以这样做——

      function test_blob_join(i_blob1 in blob, i_blob2 in blob) return blob is
        v_rt blob := empty_blob();
      begin
        dbms_lob.createtemporary(v_rt, TRUE);  -- 分配临时的 blob .
        dbms_lob.append(v_rt, i_blob1);        -- 拼接 i_blob1 .
        dbms_lob.append(v_rt, i_blob2);        -- 拼接 i_blob2 .
        return v_rt;
      end;
    

可这样测试——

    select PKG_FINGER.test_blob_join(hextoraw('0102'), hextoraw('A1A2')) from dual;
    

它返回blob的字节数据是 `01 02 A1 A2`。验证通过。

2.3 字节级操作与RAW数据类型
-----------------

现在对实现BLOB的的变量级操作是没有问题了。那么，该怎样实现BLOB的字节级操作呢？  
例如——怎么从blob中截取位置开始的一串字节？在blob中替换每个位置的字节？在blob的最后追加字节数据？  
其实dbms\_lob的 substr、write、writeappend 可分别解决这3个问题。  
然后仔细一看，会发现这些过程使用了 RAW 类型。  
对于RAW类型，很多资料是这样说的——

    RAW类型是Oracle中用于保存位串的一种数据类型，类似于CHAR，使用RAW（L) 方式声明，最长可达32767字节。
    

RAW与BLOB的关系——

*   BLOB中的一段字节数据，就是RAW类型的。例如通过 dbms\_lob.substr 截取得到的数据。
*   其次可根据 RAW数据 去替换BLOB中的某段字节数据。即使用 dbms\_lob.write 。
*   可在BLOB的最后追加 RAW数据 。即使用 dbms\_lob.writeappend 。
*   Oracle支持 RAW 隐式转型为 BLOB 类型。

观察dbms\_lob的帮助文档，会发现每个函数既有BLOB版，又有CLOB版。而且，CLOB版用VARCHAR2类型时，其BLOB版是RAW类型。即 RAW与VARCHAR2 是类似的，一个是字节串，一个是字符串。  
许多常用的字符串函数也对 RAW 是有效的。例如 length 与 sustr 。

RAW 可用十六进制字符串来表示。所以一般使用 hextoraw 函数，将十六进制字符串转为RAW。例如 `hextoraw('A1A2')` 。  
RAW 可看作十六进制字符串。所以对raw变量使用length函数时，其返回值是 字节长度的2倍（因为对于十六进制字符串，一个字节是用2个十六进制字符表示的）。substr 等函数也存在同样的情况。  
还可以用 rawtohex，将 RAW类型的数据 转换为 十六进制字符串（VARCHAR2）。

2.4 UTL\_RAW包
-------------

上面提到 RAW 的length结果是 字节长度的2倍，它是不太方便的。这时可以使用 UTL\_RAW包。例如 utl\_raw.length的结果就是 字节长度。  
常见的UTL\_RAW过程有——

*   length：长度计算函数，得到一个raw类型变量的长度，单位为字节
*   concat：拼接函数，用于拼接两个raw类型变量
*   substr：获取子串函数
*   bit\_and：位与函数
*   bit\_or：位或函数
*   bit\_xor：位异或函数
*   overlay：给指定字节赋值
*   cast\_to\_raw：字符串 转 RAW
*   cast\_to\_varchar2：RAW 转 varchar2
*   cast\_to\_nvarchar2：RAW 转 nvarchar2
*   cast\_to\_number：RAW 转 number
*   cast\_from\_number：number 转 RAW
*   cast\_to\_binary\_integer：RAW 转 binary\_integer
*   cast\_from\_binary\_integer：binary\_integer 转 RAW

三、使用心得
------

### 3.1 32位整数转换函数

最开始不知道 binary\_integer就是32位整数。于是自己写了32位整数与 RAW 的转换函数。虽然现在用不上了，但觉得它们还是很适合作为应用示范的。

      -- 将数字转为 raw(4)类型的 大端方式32位整数 .
      function TO_INT32BE(i_src in number) return raw is
        v_src number;
        v_hexstr varchar2(20);
        v_rt raw(4);
      begin
        v_src := i_src;
        if (v_src<0) then
          v_src:=v_src + 4294967296;    -- 为了支持负数.
        end if;
        v_hexstr := '0000000' || trim(to_char(v_src,'XXXXXXXX'));
        v_hexstr := substr(v_hexstr, length(v_hexstr)-7, length(v_hexstr));
        v_rt := hextoraw(v_hexstr);
        return v_rt;
      end;
      
      -- 将数字转为 raw(4)类型的 小端方式32位整数 .
      function TO_INT32LE(i_src in number) return raw is
        v_src number;
        v_hexstr varchar2(20);
        v_rt raw(4);
      begin
        v_src := i_src;
        if (v_src<0) then
          v_src:=v_src + 4294967296;    -- 为了支持负数.
        end if;
        v_hexstr := '0000000' || trim(to_char(v_src,'XXXXXXXX'));
        v_hexstr := substr(v_hexstr, length(v_hexstr)-7, length(v_hexstr));
        v_hexstr := substr(v_hexstr, 7, 2)
          || substr(v_hexstr, 5, 2)
          || substr(v_hexstr, 3, 2)
          || substr(v_hexstr, 1, 2)
          ;
        v_rt := hextoraw(v_hexstr);
        return v_rt;
      end;
      
      -- 将 存储在raw(4)中的大端方式32位整数 转为数字. 值域为 0~4294967295 .
      function FROM_INT32BE(i_src in raw) return number is
        v_src raw(8);
        v_hexstr varchar2(20):='';
        v_rt number:=0;
      begin
        if ( (nvl(length(i_src), 0)<=0) ) then
          return v_rt;
        end if;
        if (length(i_src) >= 8) then    -- length、substr均把 raw 的1个字节看作 2个（十六进制）字符.
          v_src := substr(i_src, 1, 8);
          v_hexstr := rawtohex(v_src);
        else
          v_hexstr := '000000' || rawtohex(i_src);
          v_hexstr := substr(v_hexstr, length(v_hexstr)-7, length(v_hexstr));
        end if;
        v_rt := to_number(v_hexstr,'XXXXXXXX');
        return v_rt;
      end;
      
      -- 将 存储在raw(4)中的小端方式32位整数 转为数字. 值域为 0~4294967295 .
      function FROM_INT32LE(i_src in raw) return number is
        v_src raw(8);
        v_hexstr varchar2(20):='';
        v_rt number:=0;
      begin
        if ( (nvl(length(i_src), 0)<=0) ) then
          return v_rt;
        end if;
        if (length(i_src) >= 8) then    -- length、substr均把 raw 的1个字节看作 2个（十六进制）字符.
          v_src := substr(i_src, 1, 8);
          v_hexstr := rawtohex(v_src);
        else
          v_hexstr := rawtohex(i_src) || '000000';
          v_hexstr := substr(v_hexstr, 1, 8);
        end if;
        v_hexstr := substr(v_hexstr, 7, 2)
          || substr(v_hexstr, 5, 2)
          || substr(v_hexstr, 3, 2)
          || substr(v_hexstr, 1, 2)
          ;
        v_rt := to_number(v_hexstr,'XXXXXXXX');
        return v_rt;
      end;
    

### 3.2 将32位整数追加到blob

很多时候需要给blob追加一个 32位整数。现在利用上面的函数，可以这样做——

          v_tempraw := TO_INT32LE(nvl(i_int32, 0));
          dbms_lob.writeappend(v_blob, 4, v_tempraw);
    

（完）

参考文献
----

*   Oracle Database Online Documentation / DBMS\_LOB . [https://docs.oracle.com/cd/E11882\_01/timesten.112/e21645/d\_lob.htm#TTPLP600](https://docs.oracle.com/cd/E11882_01/timesten.112/e21645/d_lob.htm#TTPLP600)
*   Oracle Database Online Documentation / UTL\_RAW . [https://docs.oracle.com/cd/E11882\_01/timesten.112/e21645/u\_raw.htm#TTPLP072](https://docs.oracle.com/cd/E11882_01/timesten.112/e21645/u_raw.htm#TTPLP072)
*   旺仔丶小馒头《DBMS\_LOB》 . [https://www.cnblogs.com/wang-chen/p/5756475.html](https://www.cnblogs.com/wang-chen/p/5756475.html)
*   Hornsey《Oracle RAW类型基本操作函数及使用示例》 . [https://blog.csdn.net/nalw2012/article/details/72466256](https://blog.csdn.net/nalw2012/article/details/72466256)
*   jimeper《ORACLE十进制与十六进制的转换》 . [https://www.cnblogs.com/jimeper/archive/2013/01/24/2875245.html](https://www.cnblogs.com/jimeper/archive/2013/01/24/2875245.html)